
# Set the minimum version of cmake required to 2.6
CMAKE_MINIMUM_REQUIRED( VERSION 2.6 )

PROJECT( netgen )

SET( PROJECT_NAMESPACE_PREFIX NETGEN )
SET( LIBRARY_TARGET_NAME nglib )
# STRING( TOUPPER ${LIBRARY_TARGET_NAME}  )
SET( NETGEN_MAJOR_VERSION "4" )
SET( NETGEN_MINOR_VERSION "9" )
SET( NETGEN_PATCH_VERSION "11" )

SET( PACKAGE_VERSION \"${NETGEN_MAJOR_VERSION}.${NETGEN_MINOR_VERSION}.${NETGEN_PATCH_VERSION}\" ) 

# Set up library options
# Generic library options
OPTION( ${PROJECT_NAMESPACE_PREFIX}_BUILD_STATIC_LIB "Build static zlib" TRUE )
OPTION( ${PROJECT_NAMESPACE_PREFIX}_INSTALL_CONFIG "Install a config file for the library" FALSE )

# Specific library options
OPTION( ${PROJECT_NAMESPACE_PREFIX}_PARALLEL_THREADS "Use parallel threads for netgen" FALSE ) 

IF( ${PROJECT_NAMESPACE_PREFIX}_BUILD_STATIC_LIB )
	SET( LIBRARY_BUILD_TYPE STATIC )
	SET( LIBRARY_INSTALL_TYPE ARCHIVE )
ELSE( ${PROJECT_NAMESPACE_PREFIX}_BUILD_STATIC_LIB )
	SET( LIBRARY_BUILD_TYPE SHARED )
	SET( LIBRARY_INSTALL_TYPE LIBRARY )
	IF( WIN32 )
		SET( LIBRARY_INSTALL_TYPE RUNTIME )
	ENDIF( WIN32 )
ENDIF( ${PROJECT_NAMESPACE_PREFIX}_BUILD_STATIC_LIB )

# Set required definitions for building netgen.  This should really
# be in a config.h header file but that's more changes.
IF( WIN32 )
	SET( NETGEN_DEFINITIONS -D_CRT_SECURE_NO_WARNINGS )
ENDIF( WIN32 )
IF( NOT ${PROJECT_NAMESPACE_PREFIX}_PARALLEL_THREADS )
	SET( NETGEN_DEFINITIONS ${NETGEN_DEFINITIONS} -DNO_PARALLEL_THREADS )
ENDIF( NOT ${PROJECT_NAMESPACE_PREFIX}_PARALLEL_THREADS )
IF( NOT ${PROJECT_NAMESPACE_PREFIX}_BUILD_STATIC_LIB )
	SET( NETGEN_DEFINITIONS ${NETGEN_DEFINITIONS} -DNGLIB_SHARED_LIB )
ENDIF( NOT ${PROJECT_NAMESPACE_PREFIX}_BUILD_STATIC_LIB )

ADD_DEFINITIONS( ${NETGEN_DEFINITIONS}  )

INCLUDE_DIRECTORIES( libsrc/include )
INCLUDE_DIRECTORIES( libsrc/general )

SET( CMAKE_DEBUG_POSTFIX "d" )
SET( CMAKE_MFC_FLAG 0 )

# Defines NGLIB_SRCS all the sources required for a stripped down library
# suitable for cmgui
INCLUDE( nglib-cmgui.cmake )

IF( ${CMAKE_SYSTEM_NAME} STREQUAL "Linux" AND ${CMAKE_SYSTEM_PROCESSOR} STREQUAL "x86_64" )
	INCLUDE( nglib-cmgui-fpic.cmake )
	SET_SOURCE_FILES_PROPERTIES(${NGLIB_SRCS}
		PROPERTIES COMPILE_FLAGS "-fPIC")
ENDIF( ${CMAKE_SYSTEM_NAME} STREQUAL "Linux" AND ${CMAKE_SYSTEM_PROCESSOR} STREQUAL "x86_64" )

# Configure header file
CONFIGURE_FILE( config.h.cmake
	${CMAKE_CURRENT_BINARY_DIR}/config/ng-config.h @ONLY )

# Add configured header file to header file search path
INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_BINARY_DIR}/config/ )

# Create library target
ADD_LIBRARY( ${LIBRARY_TARGET_NAME} ${LIBRARY_BUILD_TYPE} ${NGLIB_SRCS} )

# Set the install targets
INSTALL( TARGETS ${LIBRARY_TARGET_NAME} EXPORT ${LIBRARY_TARGET_NAME}-targets ${LIBRARY_INSTALL_TYPE}
	DESTINATION lib )
INSTALL( FILES ${CMAKE_CURRENT_SOURCE_DIR}/nglib/nglib.h
	DESTINATION include )

IF( ${PROJECT_NAMESPACE_PREFIX}_INSTALL_CONFIG )
	STRING( TOLOWER ${PROJECT_NAMESPACE_PREFIX} LOWER_PROJECT_NAMESPACE_PREFIX )
	SET( LIBRARY_CONFIG_FILE
		${CMAKE_CURRENT_BINARY_DIR}/${LOWER_PROJECT_NAMESPACE_PREFIX}-config.cmake )
	SET( CONFIG_FILE_CONTENTS 
		"\nIF( NOT DEFINED _${PROJECT_NAMESPACE_PREFIX}_CONFIG_CMAKE )" 
		"\nSET( _${PROJECT_NAMESPACE_PREFIX}_CONFIG_CMAKE TRUE )" 
	        "\nGET_FILENAME_COMPONENT( SELF_DIR \"\${CMAKE_CURRENT_LIST_FILE}\" PATH )"
	        "\nINCLUDE( \${SELF_DIR}/${LIBRARY_TARGET_NAME}-targets.cmake )"
	        "\nGET_FILENAME_COMPONENT( ${PROJECT_NAMESPACE_PREFIX}_INCLUDE_DIRS \"\${SELF_DIR}/../../include\" ABSOLUTE )"
	        "\nSET( ${PROJECT_NAMESPACE_PREFIX}_LIBRARIES ${LIBRARY_TARGET_NAME} )"
	        "\nSET( ${PROJECT_NAMESPACE_PREFIX}_DEFINITIONS )"
	        "\nSET( ${PROJECT_NAMESPACE_PREFIX}_FOUND TRUE )" 
		"\nENDIF( NOT DEFINED _${PROJECT_NAMESPACE_PREFIX}_CONFIG_CMAKE )" 
	        "\n" )
	FILE( WRITE ${LIBRARY_CONFIG_FILE} ${CONFIG_FILE_CONTENTS} )
	INSTALL( FILES ${LIBRARY_CONFIG_FILE} DESTINATION lib/cmake )
	INSTALL( EXPORT ${LIBRARY_TARGET_NAME}-targets 
		DESTINATION lib/cmake )
ENDIF( ${PROJECT_NAMESPACE_PREFIX}_INSTALL_CONFIG )

